import java.text.SimpleDateFormat

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

ext {
    environment = "prod"
}

if (hasProperty("env")) {
    environment = "$env"
}

ext.env = environment

apply from: 'tasks.gradle'

processResources.dependsOn copyConfiguration

version = "1.0-$env"
if ("prod" == env.toString()) {
    version += '-' + new SimpleDateFormat("yyyyMMdd_HHmmss").format(new Date())
}

buildscript {

    repositories {
        maven { url "http://repo.spring.io/libs-release" }
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

dependencies {

    compile project(':elastic-task-core')

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-freemarker'
    compile 'org.springframework.boot:spring-boot-starter-mail'
    compile "org.mybatis.spring.boot:mybatis-spring-boot-starter:${mybatisSpringBootStarterVersion}"
    compile "mysql:mysql-connector-java:${mysqlConnectorJavaVersion}"
    compile "org.quartz-scheduler:quartz:${quartzVersion}"

    testCompile 'org.springframework.boot:spring-boot-starter-test'

}

configurations {
    // replaced with jcl-over-slf4j
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    // replaced with log4j-over-slf4j
    all*.exclude group: 'log4j', module: 'log4j'
}
